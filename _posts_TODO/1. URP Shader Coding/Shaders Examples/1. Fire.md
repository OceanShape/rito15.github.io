
<!-- 2022. 05. 05. 목 -->

# 불 쉐이더
---

```hlsl
Shader "URP Study/Fire"
{
    Properties
    {
        _FireTex    ("Fire Texture"   , 2D) = "white" {}
        _NoiseTex   ("Noise Texture"  , 2D) = "white" {}
        _FlowSpeed  ("Flow Speed"     , Range(0, 5)) = 0.3
        _Intensity  ("Color Intensity", Range(0, 5)) = 1
        _Alpha      ("Alpha"          , Range(0, 5)) = 1
        _NoiseScale ("Noise Scale"    , Range(0, 10)) = 1
        _ShakeScale ("Shake Scale"    , Range(0, 10)) = 1
    }
    SubShader
    {
        Tags 
        { 
            "RenderPipeline" = "UniversalPipeline"
            "RenderType"     = "Transparent"
            "Queue"          = "Transparent"
        }
        Pass
        {
            Blend SrcAlpha OneMinusSrcAlpha

            Tags
            {
                "LightMode" = "UniversalForward"
            }
            HLSLPROGRAM

            #pragma prefer_hlslcc gles
            #pragma vertex vert
            #pragma fragment frag
            
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"

            struct vi
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float2 uv : TEXCOORD0;
                float2 uv2 : TEXCOORD1;
                float4 vertex : SV_POSITION;
            };

            sampler2D _FireTex;
            sampler2D _NoiseTex;
            float4 _FireTex_ST;
            float4 _NoiseTex_ST;

            half _FlowSpeed;
            half _Intensity;
            half _Alpha;
            half _NoiseScale;
            half _ShakeScale;

            v2f vert (vi i)
            {
                v2f o;
                o.vertex = TransformObjectToHClip(i.vertex);
                o.uv     = TRANSFORM_TEX(i.uv, _FireTex);
                o.uv2    = TRANSFORM_TEX(i.uv, _NoiseTex); // 노이즈 전용 UV 전달(노이즈 텍스쳐 ST 계산)
                return o;
            }

            half4 frag (v2f i) : SV_Target
            {
                half4 o4 = 0.;

                // 시간값
                half2 t2 = half2(0, -_Time.y * _FlowSpeed);

                // 노이즈값
                half n = tex2D(_NoiseTex, i.uv2 * _NoiseScale + t2).r;

                // 메인 텍스쳐 UV.xy에 더해줄 노이즈 기반 흔들림값
                // 그냥 (_ShakeScale * n)을 uv에 더해주면 텍스쳐 색상이 우상단으로 이동하므로, -0.5로 보정해준다.
                half s = _ShakeScale * (n - 0.5);

                // 불 샘플링
                half4 f4 = tex2D(_FireTex, i.uv + s);

                // 최종 색상 계산
                o4.rgb = f4.rgb * _Intensity;
                o4.a = saturate(f4.a * n * _Alpha);

                return o4;
            }
            ENDHLSL
        }
    }
}
```