
# 기본 구조
---
```hlsl
Shader "ShaderCategory/ShaderName"
{
    Properties
    {
        // Properties Block
    }
    SubShader
    {
        Tags
        {
            // Render type, Render Queue 결정
            "RenderPipeline"="UniversalPipeline"
            "RenderType"="Opaque"
            "Queue"="Geometry"
        }
        Pass
        {
            Name "Universal Forward"
            Tags { "LightMode" = "UniversalForward" }
            
            HLSLPROGRAM
            
            #pragma prefer_hlslcc gles
            #pragma exclude_renderers d3d11_9x
            #pragma vertex vert
            #pragma fragment frag
            
            // Use shader model 3.0 target, to get nicer looking lighting
            #pragma target 3.0
            
            // cg shader : .cginc / hlsl shader : .hlsl
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"
            
            // Vertex Buffer에서 읽어올 데이터
            struct VertexInput
            {
                float4 vertex : POSITION;
            };
            
            // 보간기를 통해 버텍스 셰이더에서 픽셀 셰이더로 전달할 데이터
            struct VertexOutput
            {
                float4 vertex : SV_POSITION;
            };
            
            // 쉐이더 내에서 사용할 변수들(fixed 타입 안됨)
            sampler2D _MainTex;
            half2 _MainTex_ST;
            half4 _Color;
            
            // 버텍스 셰이더
            VertexOutput vert(VertexInput v)
            {
                VertexOutput o;
                o.vertex = TransformObjectToHClip(v.vertex.xyz);
                return o;
            }
            
            // 픽셀 셰이더
            half4 frag(VertexOutput i) : SV_Target
            {
                return half4(0.5 , 0.5, 0.5, 1);
            }
            
            ENDHLSL
        }
    }
}
```

<br>


# Properties
---

```hlsl
Properties
{
    _MyColor   ("Some Color" , Color)       = (1,1,1,1)
    _MyVector  ("Some Vector", Vector)      = (0,0,0,0)
    _MyInt     ("My Int"     , Int)         = 1
    _MyFloat   ("My Float"   , Float)       = 0.5
    _MyRange   ("My Range"   , Range(0, 1)) = 0.5
    
    // Texture Sampler 타입들
    _MyTexture ("Texture2D"  , 2D)          = "white" {} // white, gray, black, bump
    _MyTex3D   ("Texture3D"  , 3D)          = "white" {}
    _MyTexRect ("Rect"       , Rect)        = "white" {} // UI 이미지 등에 사용
    _MyCubemap ("Cubemap"    , CUBE)        = "white" {}
}
```

<br>


# Tags
---

```hlsl
Tags
{
    "RenderPipeline" = "UniversalPipeline"
    "RenderType"     = "Opaque"
    "Queue"          = "Geometry"
}
```

<br>

## Render Type
> 미리 정의된 그룹들로 분류되어, 카메라 뎁스 텍스쳐 등에 사용된다.

- Opaque                : 불투명
- Transparent           : 반투명
- TransparentCutout     : 알파 테스트
- Background            : 스카이박스
- Overlay               : Halo, Lens Flare

- TreeOpaque            : 터레인 나무, 줄기 부분
- TreeTransparentCutout : 터레인 나뭇잎 부분
- TreeBillboard         : 터레인 빌보드 나무
- Grass                 : 터레인 잔디
- GrassBillboard        : 터레인 빌보드 잔디

<br>

## Queue
> 오브젝트 그리는 순서 결정

- Background  (1000) : 가장 먼저 그려짐
- Geometry    (2000) : 불투명
- AlphaTest   (2450) : 알파 테스트
- Transparent (3000) : 반투명
- Overlay     (4000) : 렌즈 플레어

<br>


# Pragma
---
> C언어에서 컴파일러가 입력을 처리하는 방법을 지정하는 전처리 문법

<br>

## prefer_hlslcc gles
- 유니티에서는 다양한 쉐이더 컴파일러, 크로스 컴파일러를 사용하는데
  SRP에서는 HLSLcc를 사용한다.
  
- 안드로이드처럼 OpenGLES 그래픽스 API를 사용하는 플랫폼에서는
  HLSLcc가 기본적으로 사용되지 않으며, 이 명령어를 통해 강제로 실행해야 한다.



